/-
Copyright (c) 2023 Scott Morrison. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Scott Morrison
-/
import Mathlib.Data.ListM.Basic

/-!
# Best first search

We perform best first search of a tree or graph,
where the neighbours of a vertex are provided by a lazy list `α → ListM m α`.

We maintain a priority queue of visited-but-not-exhausted nodes,
and at each step take the next child of the highest priority node in the queue.

This is useful in meta code for searching for solutions in the presence of alternatives.
It can be nice to represent the choices via a lazy list,
so the later choices don't need to be evaluated while we do depth first search on earlier choices.

Options:
* `maxDepth` allows bounding the search depth
* `maxQueued` implements "beam" search,
  by discarding elements from the priority queue when it grows too large
* `removeDuplicates` maintains an `RBSet` of previously visited nodes;
  otherwise if the graph is not a tree nodes may be visited multiple times.
-/

open Std

/--
View a type `Q` as a queue of `α`.

We can push an element on to the queue or pop an element off.
No guarantees about the order is implied
(or indeed any relation between pushed and popped elements).
-/
-- If this proves useful elsewhere it can be moved up the import hierarchy.
class Queue (α : outParam (Type u)) (Q : Type u) where
  empty : Q
  push : Q → α → Q
  pop : Q → Option (α × Q)

instance (α : Type _) : Queue α (List α) where
  empty := []
  push Q a := a :: Q
  pop := fun
  | [] => none
  | h :: t => some (h, t)

/--
View an `RBMap α β compare` as a `Queue` of `α × β`,
popping pairs with the least `α` component first.
If `bound = some n`, then when pushing a pair would cause the length of the queue to exceed `n`,
the pair with largest `α` component is discarded.

Note that enqueuing multiple elements with the same first component will discard the earlier ones.
-/
def rbMapQueue (α β : Type u) [Ord α] (bound : Option Nat := none) :
    Queue (α × β) (RBMap α β compare) where
  empty := ∅
  push Q := fun ⟨a, b⟩ =>
    let R := Q.insert a b
    match bound with
    | none => R
    | some b => if R.size ≤ b then R else match R.max with
      | none => unreachable!
      | some (a', _) => R.erase a'
  pop Q := match Q.min with
  | none => none
  | some (a, b) => some ((a, b), Q.erase a)

variable {α : Type u} [Monad m] [Alternative m]

open ListM Queue

/--
Auxiliary function for `bestFirstSearch`, that updates the internal state,
consisting of a priority queue of triples `α × Nat × ListM m α`.
We remove the next element from the list contained in the best triple
(discarding the triple if there is no next element),
enqueue it and return it.
-/
-- The return type has `× List α` rather than just `× Option α` so `bestFirstSearch` can use `fixl`.
def bestFirstSearchAux {Q : Type u} [Queue (α × (Nat × ListM m α)) Q]
    (f : Nat → α → ListM m α) (s : Q) : m (Q × List α) := do
  match Queue.pop s with
  | none => failure
  | some ((a, (n, L)), s') =>
    match ← uncons L with
    | none => pure (s', [])
    | some (b, L') => do
      let s' := Queue.push (Queue.push s' (a, (n, L'))) (b, (n + 1, f (n+1) b))
      pure (s', [b])

variable [Ord α]

/--
Implementation of the `bestFirstSearch` function,
allowing for an arbitrary `Queue` data structure which handles prioritization.
-/
def bestFirstSearchImpl (Q : Type u → Type u) [∀ β : Type u, Queue (α × β) (Q β)]
    (f : α → ListM m α) (a : α)
    (maxDepth : Option Nat := none) (removeDuplicates := true) :
    ListM m α :=
  let f := match maxDepth with
  | none => fun _ a => f a
  | some d => fun n a => if d < n then empty else f a
  if removeDuplicates then
    let f' : Nat → α → ListM (StateT.{u} (RBSet α compare) m) α := fun n a =>
      (f n a).liftM >>= fun b => do
        let s ← get
        if s.contains b then failure
        set <| s.insert b
        pure b
    let init := Queue.push (Queue.empty : Q (Nat × _)) (a, (0, f' 0 a))
    cons (do pure (some a, fixl (bestFirstSearchAux f') init))
      |>.runState' (RBSet.empty.insert a)
  else
    let init := Queue.push (Queue.empty : Q (Nat × _)) (a, (0, f 0 a))
    cons do pure (some a, fixl (bestFirstSearchAux f) init)

/--
A lazy list recording the best first search of a graph generated by a function
`f : α → ListM m α`.

We maintain a priority queue of visited-but-not-exhausted nodes,
and at each step take the next child of the highest priority node in the queue.

The option `maxDepth` limits the search depth.

The option `maxQueued` bounds the size of the priority queue,
discarding the lowest priority nodes as needed.
This implements a "beam" search, which may be incomplete but uses bounded memory.

The option `removeDuplicates` keeps an `RBSet` of previously visited nodes.
Otherwise, if the graph is not a tree then nodes will be visited multiple times.
-/
def bestFirstSearch (f : α → ListM m α) (a : α)
    (maxDepth : Option Nat := none) (maxQueued : Option Nat := none) (removeDuplicates := true) :
    ListM m α :=
  have := fun (β : Type u) => rbMapQueue α β maxQueued
  bestFirstSearchImpl (fun β => RBMap α β compare) f a maxDepth removeDuplicates
