/-
Copyright (c) 2023 Moritz Doll. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Moritz Doll, S√©bastien Gou√´zel, Jireh Loreaux
-/

import Mathlib.Analysis.MeanInequalities

/-!
# `L^p` distance on products of two metric spaces
Given two metric spaces, one can put the max distance on their product, but there is also
a whole family of natural distances, indexed by a parameter `p : ‚Ñù‚â•0‚àû`, that also induce
the product topology. We define them in this file. For `0 < p < ‚àû`, the distance on `Œ± √ó Œ≤`
is given by
$$
d(x, y) = \left(d(x_1, y_1)^p + d(x_2, y_2)^p\right)^{1/p}.
$$
For `p = ‚àû` the distance is the supremum of the distances.

We give instances of this construction for emetric spaces, metric spaces, normed groups and normed
spaces.

To avoid conflicting instances, all these are defined on a copy of the original Prod-type, named
`ProdLp p Œ± Œ≤`. The assumption `[Fact (1 ‚â§ p)]` is required for the metric and normed space
instances.

We ensure that the topology, bornology and uniform structure on `ProdLp p Œ± Œ≤` are (defeq to) the
product topology, product bornology and product uniformity, to be able to use freely continuity
statements for the coordinate functions, for instance.

# Implementation notes

This files is a straight-forward adaption of `Mathlib.Analysis.NormedSpace.PiLp`. We deviate from
`PiLp` in that we use for `p = 0` the junk value `d(x, y) = 0`.

-/

local macro_rules | `($x ^ $y)   => `(HPow.hPow $x $y) -- Porting note: See issue #2220

open Real Set Filter IsROrC Bornology BigOperators Uniformity Topology NNReal ENNReal

noncomputable section

/-- A copy of a Prod type, on which we will put the `L^p` distance. Since the Prod type itself is
already endowed with the `L^‚àû` distance, we need the type synonym to avoid confusing typeclass
resolution. Also, we let it depend on `p`, to get a whole family of type on which we can put
different distances. -/
@[nolint unusedArguments]
def ProdLp (_p : ‚Ñù‚â•0‚àû) (Œ± Œ≤ : Type _) : Type _ :=
  Œ± √ó Œ≤

instance ProdLp.instInhabited (p : ‚Ñù‚â•0‚àû) (Œ± Œ≤ : Type _) [Inhabited Œ±] [Inhabited Œ≤] :
    Inhabited (ProdLp p Œ± Œ≤) :=
  ‚ü®default, default‚ü©

@[ext]
protected theorem ProdLp.ext {p : ‚Ñù‚â•0‚àû} {Œ± Œ≤ : Type _} {x y : ProdLp p Œ± Œ≤}
    (h1 : x.fst = y.fst) (h2 : x.snd = y.snd) : x = y := Prod.ext h1 h2

namespace ProdLp

variable (p : ‚Ñù‚â•0‚àû) (ùïú ùïú' : Type _) (Œ± Œ≤ : Type _)

/-- Canonical bijection between `ProdLp p Œ± Œ≤` and the original Prod type. We introduce it to be
able to compare the `L^p` and `L^‚àû` distances through it. -/
protected def equiv : ProdLp p Œ± Œ≤ ‚âÉ Œ± √ó Œ≤ :=
  Equiv.refl _

/-! Note that the unapplied versions of these lemmas are deliberately omitted, as they break
the use of the type synonym. -/

@[simp]
theorem equiv_apply (x : ProdLp p Œ± Œ≤) : ProdLp.equiv p Œ± Œ≤ x = x :=
  rfl

@[simp]
theorem equiv_symm_apply (x : Œ± √ó Œ≤) : (ProdLp.equiv p Œ± Œ≤).symm x = x :=
  rfl

section DistNorm

/-!
### Definition of `edist`, `dist` and `norm` on `ProdLp`

In this section we define the `edist`, `dist` and `norm` functions on `ProdLp p Œ± Œ≤` without
assuming `[Fact (1 ‚â§ p)]` or metric properties of the spaces `Œ±` and `Œ≤`. This allows us to provide
the rewrite lemmas for each of three cases `p = 0`, `p = ‚àû` and `0 < p.to_real`.
-/


section Edist

variable [EDist Œ±] [EDist Œ≤]

/-- Endowing the space `ProdLp p Œ± Œ≤` with the `L^p` edistance. We register this instance
separate from `ProdLp.instPseudoEMetric` since the latter requires the type class hypothesis
`[Fact (1 ‚â§ p)]` in order to prove the triangle inequality.

Registering this separately allows for a future emetric-like structure on `ProdLp p Œ± Œ≤` for `p < 1`
satisfying a relaxed triangle inequality. The terminology for this varies throughout the
literature, but it is sometimes called a *quasi-metric* or *semi-metric*. -/
instance instEDist : EDist (ProdLp p Œ± Œ≤) where
  edist f g :=
    if _hp : p = 0 then 0
    else
      if p = ‚àû then edist f.fst g.fst ‚äî edist f.snd g.snd
      else (edist f.fst g.fst ^ p.toReal + edist f.snd g.snd ^ p.toReal) ^ (1 / p.toReal)

variable {Œ± Œ≤}

variable (x y : ProdLp p Œ± Œ≤) (x' : Œ± √ó Œ≤)

@[simp]
protected theorem edist_eq_zero (f g : ProdLp 0 Œ± Œ≤) : edist f g = 0 :=
  if_pos rfl

theorem edist_eq_add {p : ‚Ñù‚â•0‚àû} (hp : 0 < p.toReal) (f g : ProdLp p Œ± Œ≤) :
    edist f g = (edist f.fst g.fst ^ p.toReal + edist f.snd g.snd ^ p.toReal) ^ (1 / p.toReal) :=
  let hp' := ENNReal.toReal_pos_iff.mp hp
  (if_neg hp'.1.ne').trans (if_neg hp'.2.ne)

theorem edist_eq_sup (f g : ProdLp ‚àû Œ± Œ≤) :
    edist f g = edist f.fst g.fst ‚äî edist f.snd g.snd := by
  dsimp [edist]
  exact if_neg ENNReal.top_ne_zero

end Edist

section EdistProp

variable {Œ± Œ≤}
variable [PseudoEMetricSpace Œ±] [PseudoEMetricSpace Œ≤]

/-- This holds independent of `p` and does not require `[Fact (1 ‚â§ p)]`. We keep it separate
from `ProdLp.instPseudoEMetricSpace` so it can be used also for `p < 1`. -/
protected theorem edist_self (f : ProdLp p Œ± Œ≤) : edist f f = 0 := by
  rcases p.trichotomy with (rfl | rfl | h)
  ¬∑ simp
  ¬∑ simp [edist_eq_sup]
  ¬∑ simp [edist_eq_add h, ENNReal.zero_rpow_of_pos h, ENNReal.zero_rpow_of_pos (inv_pos.2 <| h)]

/-- This holds independent of `p` and does not require `[Fact (1 ‚â§ p)]`. We keep it separate
from `ProdLp.instPseudoEMetricSpace` so it can be used also for `p < 1`. -/
protected theorem edist_comm (f g : ProdLp p Œ± Œ≤) : edist f g = edist g f := by
  rcases p.trichotomy with (rfl | rfl | h)
  ¬∑ simp only [ProdLp.edist_eq_zero, eq_comm, Ne.def]
  ¬∑ simp only [edist_eq_sup, edist_comm]
  ¬∑ simp only [edist_eq_add h, edist_comm]

end EdistProp

section Dist

variable [Dist Œ±] [Dist Œ≤]

/-- Endowing the space `ProdLp p Œ± Œ≤` with the `L^p` distance. We register this instance
separate from `ProdLp.instPseudoMetricSpace` since the latter requires the type class hypothesis
`[Fact (1 ‚â§ p)]` in order to prove the triangle inequality.

Registering this separately allows for a future metric-like structure on `ProdLp p Œ± Œ≤` for `p < 1`
satisfying a relaxed triangle inequality. The terminology for this varies throughout the
literature, but it is sometimes called a *quasi-metric* or *semi-metric*. -/
instance instDist : Dist (ProdLp p Œ± Œ≤) where
  dist f g :=
    if _hp : p = 0 then 0 --{ i | f i ‚â† g i }.toFinite.toFinset.card
    else
      if p = ‚àû then dist f.fst g.fst ‚äî dist f.snd g.snd
      else (dist f.fst g.fst ^ p.toReal + dist f.snd g.snd ^ p.toReal) ^ (1 / p.toReal)

variable {Œ± Œ≤}

theorem dist_eq_card (f g : ProdLp 0 Œ± Œ≤) : dist f g = 0 :=
  if_pos rfl

theorem dist_eq_add {p : ‚Ñù‚â•0‚àû} (hp : 0 < p.toReal) (f g : ProdLp p Œ± Œ≤) :
    dist f g = (dist f.fst g.fst ^ p.toReal + dist f.snd g.snd ^ p.toReal) ^ (1 / p.toReal) :=
  let hp' := ENNReal.toReal_pos_iff.mp hp
  (if_neg hp'.1.ne').trans (if_neg hp'.2.ne)

theorem dist_eq_sup (f g : ProdLp ‚àû Œ± Œ≤) :
    dist f g = dist f.fst g.fst ‚äî dist f.snd g.snd := by
  dsimp [dist]
  exact if_neg ENNReal.top_ne_zero

end Dist

section Norm

variable [Norm Œ±] [Zero Œ±] [Norm Œ≤] [Zero Œ≤]

/-- Endowing the space `ProdLp p Œ± Œ≤` with the `L^p` norm. We register this instance
separate from `ProdLp.instSeminormedAddCommGroup` since the latter requires the type class
hypothesis `[Fact (1 ‚â§ p)]` in order to prove the triangle inequality.

Registering this separately allows for a future norm-like structure on `ProdLp p Œ± Œ≤` for `p < 1`
satisfying a relaxed triangle inequality. These are called *quasi-norms*. -/
instance instNorm : Norm (ProdLp p Œ± Œ≤) where
  norm f :=
    if _hp : p = 0 then 0
    else if p = ‚àû then ‚Äñf.fst‚Äñ ‚äî ‚Äñf.snd‚Äñ
    else (‚Äñf.fst‚Äñ ^ p.toReal + ‚Äñf.snd‚Äñ ^ p.toReal) ^ (1 / p.toReal)

variable {p Œ± Œ≤}

@[simp]
protected theorem norm_eq_zero (f : ProdLp 0 Œ± Œ≤) : ‚Äñf‚Äñ = 0 :=
  if_pos rfl

theorem norm_eq_sup (f : ProdLp ‚àû Œ± Œ≤) : ‚Äñf‚Äñ = ‚Äñf.fst‚Äñ ‚äî ‚Äñf.snd‚Äñ := by
  dsimp [Norm.norm]
  exact if_neg ENNReal.top_ne_zero

theorem norm_eq_add (hp : 0 < p.toReal) (f : ProdLp p Œ± Œ≤) :
    ‚Äñf‚Äñ = (‚Äñf.fst‚Äñ ^ p.toReal + ‚Äñf.snd‚Äñ ^ p.toReal) ^ (1 / p.toReal) :=
  let hp' := ENNReal.toReal_pos_iff.mp hp
  (if_neg hp'.1.ne').trans (if_neg hp'.2.ne)

end Norm

end DistNorm

section Aux

/-!
### The uniformity on finite `L^p` products is the product uniformity

In this section, we put the `L^p` edistance on `ProdLp p Œ± Œ≤`, and we check that the uniformity
coming from this edistance coincides with the product uniformity, by showing that the canonical
map to the Pi type (with the `L^‚àû` distance) is a uniform embedding, as it is both Lipschitz and
antiLipschitz.

We only register this emetric space structure as a temporary instance, as the true instance (to be
registered later) will have as uniformity exactly the product uniformity, instead of the one coming
from the edistance (which is equal to it, but not defeq). See Note [forgetful inheritance]
explaining why having definitionally the right uniformity is often important.
-/


variable [Fact (1 ‚â§ p)]

/-- Endowing the space `ProdLp p Œ± Œ≤` with the `L^p` pseudoemetric structure. This definition is not
satisfactory, as it does not register the fact that the topology and the uniform structure coincide
with the product one. Therefore, we do not register it as an instance. Using this as a temporary
pseudoemetric space instance, we will show that the uniform structure is equal (but not defeq) to
the product one, and then register an instance in which we replace the uniform structure by the
product one using this pseudoemetric space and `PseudoEMetricSpace.replaceUniformity`. -/
def pseudoEmetricAux [PseudoEMetricSpace Œ±] [PseudoEMetricSpace Œ≤] :
    PseudoEMetricSpace (ProdLp p Œ± Œ≤) where
  edist_self := ProdLp.edist_self p
  edist_comm := ProdLp.edist_comm p
  edist_triangle f g h := by
    rcases p.dichotomy with (rfl | hp)
    ¬∑ simp only [edist_eq_sup]
      exact sup_le ((edist_triangle _ g.fst _).trans <| add_le_add le_sup_left le_sup_left)
        ((edist_triangle _ g.snd _).trans <| add_le_add le_sup_right le_sup_right)
    ¬∑ simp only [edist_eq_add (zero_lt_one.trans_le hp)]
      calc
        (edist f.fst h.fst ^ p.toReal + edist f.snd h.snd ^ p.toReal) ^ (1 / p.toReal) ‚â§
            ((edist f.fst g.fst + edist g.fst h.fst) ^ p.toReal +
              (edist f.snd g.snd + edist g.snd h.snd) ^ p.toReal) ^ (1 / p.toReal) := by
          apply ENNReal.rpow_le_rpow _ (one_div_nonneg.2 <| zero_le_one.trans hp)
          exact add_le_add (ENNReal.rpow_le_rpow (edist_triangle _ _ _) (zero_le_one.trans hp))
            (ENNReal.rpow_le_rpow (edist_triangle _ _ _) (zero_le_one.trans hp))
        _ ‚â§
            (edist f.fst g.fst ^ p.toReal + edist f.snd g.snd ^ p.toReal) ^ (1 / p.toReal) +
              (edist g.fst h.fst ^ p.toReal + edist g.snd h.snd ^ p.toReal) ^ (1 / p.toReal) := by
          have := ENNReal.Lp_add_le {0, 1}
            (if ¬∑ = 0 then edist f.fst g.fst else edist f.snd g.snd)
            (if ¬∑ = 0 then edist g.fst h.fst else edist g.snd h.snd) hp
          simp only [Finset.mem_singleton, not_false_eq_true, Finset.sum_insert,
            Finset.sum_singleton] at this
          exact this

attribute [local instance] ProdLp.pseudoEmetricAux

/-- An auxiliary lemma used twice in the proof of `ProdLp.pseudoMetricAux` below. Not intended for
use outside this file. -/
theorem sup_edist_ne_top_aux {Œ± Œ≤ : Type _}
    [PseudoMetricSpace Œ±] [PseudoMetricSpace Œ≤] (f g : ProdLp ‚àû Œ± Œ≤) :
    edist f.fst g.fst ‚äî edist f.snd g.snd ‚â† ‚ä§ := by
  refine ne_of_lt ?_
  simp [edist, PseudoMetricSpace.edist_dist]

/-- Endowing the space `ProdLp p Œ± Œ≤` with the `L^p` pseudometric structure. This definition is not
satisfactory, as it does not register the fact that the topology, the uniform structure, and the
bornology coincide with the product ones. Therefore, we do not register it as an instance. Using
this as a temporary pseudoemetric space instance, we will show that the uniform structure is equal
(but not defeq) to the product one, and then register an instance in which we replace the uniform
structure and the bornology by the product ones using this pseudometric space,
`PseudoMetricSpace.replaceUniformity`, and `PseudoMetricSpace.replaceBornology`.

See note [reducible non-instances] -/
@[reducible]
def pseudoMetricAux [PseudoMetricSpace Œ±] [PseudoMetricSpace Œ≤] :
    PseudoMetricSpace (ProdLp p Œ± Œ≤) :=
  PseudoEMetricSpace.toPseudoMetricSpaceOfDist dist
    (fun f g => by
      rcases p.dichotomy with (rfl | h)
      ¬∑ exact sup_edist_ne_top_aux f g
      ¬∑ rw [edist_eq_add (zero_lt_one.trans_le h)]
        refine
          ENNReal.rpow_ne_top_of_nonneg (by positivity) (ne_of_lt ?_)
        simp [ENNReal.add_lt_top, ENNReal.rpow_lt_top_of_nonneg, edist_ne_top] )
    fun f g => by
    rcases p.dichotomy with (rfl | h)
    ¬∑ rw [edist_eq_sup, dist_eq_sup]
      refine' le_antisymm (sup_le _ _) _
      ¬∑ rw [‚Üê ENNReal.ofReal_le_iff_le_toReal (sup_edist_ne_top_aux f g), ‚Üê
          PseudoMetricSpace.edist_dist]
        exact le_sup_left
      ¬∑ rw [‚Üê ENNReal.ofReal_le_iff_le_toReal (sup_edist_ne_top_aux f g), ‚Üê
          PseudoMetricSpace.edist_dist]
        exact le_sup_right
      ¬∑ refine ENNReal.toReal_le_of_le_ofReal ?_ ?_
        ¬∑ simp only [ge_iff_le, le_sup_iff, dist_nonneg]
        ¬∑ simp [edist, PseudoMetricSpace.edist_dist, ENNReal.ofReal_le_ofReal]
    ¬∑ have h1 : edist f.fst g.fst ^ p.toReal ‚â† ‚ä§ :=
        ENNReal.rpow_ne_top_of_nonneg (zero_le_one.trans h) (edist_ne_top _ _)
      have h2 : edist f.snd g.snd ^ p.toReal ‚â† ‚ä§ :=
        ENNReal.rpow_ne_top_of_nonneg (zero_le_one.trans h) (edist_ne_top _ _)
      simp only [edist_eq_add (zero_lt_one.trans_le h), dist_edist, ENNReal.toReal_rpow,
        dist_eq_add (zero_lt_one.trans_le h), ‚Üê ENNReal.toReal_add h1 h2]

attribute [local instance] ProdLp.pseudoMetricAux

theorem lipschitzWith_equiv_aux [PseudoEMetricSpace Œ±] [PseudoEMetricSpace Œ≤] :
    LipschitzWith 1 (ProdLp.equiv p Œ± Œ≤) := by
  intro x y
  rcases p.dichotomy with (rfl | h)
  ¬∑ simp only [equiv_apply, coe_one, one_mul, le_refl]
  ¬∑ have cancel : p.toReal * (1 / p.toReal) = 1 := mul_div_cancel' 1 (zero_lt_one.trans_le h).ne'
    rw [edist_eq_add (zero_lt_one.trans_le h)]
    simp only [edist, forall_prop_of_true, one_mul, ENNReal.coe_one, equiv_apply, ge_iff_le,
      sup_le_iff]
    constructor
    ¬∑ calc
        edist x.fst y.fst ‚â§ (edist x.fst y.fst ^ p.toReal) ^ (1 / p.toReal) := by
          simp only [‚Üê ENNReal.rpow_mul, cancel, ENNReal.rpow_one, le_refl]
        _ ‚â§ (edist x.fst y.fst ^ p.toReal + edist x.snd y.snd ^ p.toReal) ^ (1 / p.toReal) := by
          apply ENNReal.rpow_le_rpow _ (by positivity)
          simp only [self_le_add_right]
    ¬∑ calc
        edist x.snd y.snd ‚â§ (edist x.snd y.snd ^ p.toReal) ^ (1 / p.toReal) := by
          simp only [‚Üê ENNReal.rpow_mul, cancel, ENNReal.rpow_one, le_refl]
        _ ‚â§ (edist x.fst y.fst ^ p.toReal + edist x.snd y.snd ^ p.toReal) ^ (1 / p.toReal) := by
          apply ENNReal.rpow_le_rpow _ (by positivity)
          simp only [self_le_add_left]

example (a : ENNReal) : a + a = 2*a := by exact Eq.symm (two_mul a)

theorem antilipschitzWith_equiv_aux [PseudoEMetricSpace Œ±] [PseudoEMetricSpace Œ≤] :
    AntilipschitzWith ((2 : ‚Ñù‚â•0) ^ (1 / p).toReal) (ProdLp.equiv p Œ± Œ≤) := by
  intro x y
  rcases p.dichotomy with (rfl | h)
  ¬∑ simp [edist]
  ¬∑ have pos : 0 < p.toReal := by positivity
    have nonneg : 0 ‚â§ 1 / p.toReal := by positivity
    have cancel : p.toReal * (1 / p.toReal) = 1 := mul_div_cancel' 1 (ne_of_gt pos)
    rw [edist_eq_add pos, ENNReal.toReal_div 1 p]
    simp only [edist, ‚Üê one_div, ENNReal.one_toReal]
    calc
      (edist x.fst y.fst ^ p.toReal + edist x.snd y.snd ^ p.toReal) ^ (1 / p.toReal) ‚â§
          (edist (ProdLp.equiv p Œ± Œ≤ x) (ProdLp.equiv p Œ± Œ≤ y) ^ p.toReal +
          edist (ProdLp.equiv p Œ± Œ≤ x) (ProdLp.equiv p Œ± Œ≤ y) ^ p.toReal) ^ (1 / p.toReal) := by
        refine ENNReal.rpow_le_rpow (add_le_add ?_ ?_) nonneg
        ¬∑ refine ENNReal.rpow_le_rpow ?_ (le_of_lt pos)
          simp [edist]
        ¬∑ refine ENNReal.rpow_le_rpow ?_ (le_of_lt pos)
          simp [edist]
      _ =
          ((2 : ‚Ñù‚â•0) ^ (1 / p.toReal) : ‚Ñù‚â•0) *
            edist (ProdLp.equiv p Œ± Œ≤ x) (ProdLp.equiv p Œ± Œ≤ y) := by
        simp only [equiv_apply, ‚Üê two_mul, ENNReal.mul_rpow_of_nonneg _ _ nonneg,
          ‚Üê ENNReal.rpow_mul, cancel, ENNReal.rpow_one, ‚Üê ENNReal.coe_rpow_of_nonneg _ nonneg,
          coe_ofNat]

theorem aux_uniformity_eq [PseudoEMetricSpace Œ±] [PseudoEMetricSpace Œ≤] :
    ùì§ (ProdLp p Œ± Œ≤) = ùì§[instUniformSpaceProd] := by
  have A : UniformInducing (ProdLp.equiv p Œ± Œ≤) :=
    (antilipschitzWith_equiv_aux p Œ± Œ≤).uniformInducing
      (lipschitzWith_equiv_aux p Œ± Œ≤).uniformContinuous
  have : (fun x : ProdLp p Œ± Œ≤ √ó ProdLp p Œ± Œ≤ =>
    ((ProdLp.equiv p Œ± Œ≤) x.fst, (ProdLp.equiv p Œ± Œ≤) x.snd)) = id :=
    by ext i <;> rfl
  rw [‚Üê A.comap_uniformity, this, comap_id]

theorem aux_cobounded_eq [PseudoMetricSpace Œ±] [PseudoMetricSpace Œ≤] :
    cobounded (ProdLp p Œ± Œ≤) = @cobounded _ Prod.instBornology :=
  calc
    cobounded (ProdLp p Œ± Œ≤) = comap (ProdLp.equiv p Œ± Œ≤) (cobounded _) :=
      le_antisymm (antilipschitzWith_equiv_aux p Œ± Œ≤).tendsto_cobounded.le_comap
        (lipschitzWith_equiv_aux p Œ± Œ≤).comap_cobounded_le
    _ = _ := comap_id

end Aux

/-! ### Instances on `L^p` products -/


instance instUniformSpace [UniformSpace Œ±] [UniformSpace Œ≤] : UniformSpace (ProdLp p Œ± Œ≤) :=
  instUniformSpaceProd

theorem uniformContinuous_equiv [UniformSpace Œ±] [UniformSpace Œ≤] :
    UniformContinuous (ProdLp.equiv p Œ± Œ≤) :=
  uniformContinuous_id

theorem uniformContinuous_equiv_symm [UniformSpace Œ±] [UniformSpace Œ≤] :
    UniformContinuous (ProdLp.equiv p Œ± Œ≤).symm :=
  uniformContinuous_id

@[continuity]
theorem continuous_equiv [UniformSpace Œ±] [UniformSpace Œ≤] : Continuous (ProdLp.equiv p Œ± Œ≤) :=
  continuous_id

@[continuity]
theorem continuous_equiv_symm [UniformSpace Œ±] [UniformSpace Œ≤] :
    Continuous (ProdLp.equiv p Œ± Œ≤).symm :=
  continuous_id

instance instBornology [Bornology Œ±] [Bornology Œ≤] : Bornology (ProdLp p Œ± Œ≤) :=
  Prod.instBornology

-- throughout the rest of the file, we assume `1 ‚â§ p`
variable [Fact (1 ‚â§ p)]

/-- `PseudoEMetricSpace` instance on the product of two pseudoemetric spaces, using the
`L^p` pseudoedistance, and having as uniformity the product uniformity. -/
instance instPseudoEMetricSpace [PseudoEMetricSpace Œ±] [PseudoEMetricSpace Œ≤] :
    PseudoEMetricSpace (ProdLp p Œ± Œ≤) :=
  (pseudoEmetricAux p Œ± Œ≤).replaceUniformity (aux_uniformity_eq p Œ± Œ≤).symm

/-- `EMetricSpace` instance on the product of two emetric spaces, using the `L^p`
edistance, and having as uniformity the product uniformity. -/
instance instEMetricSpace [EMetricSpace Œ±] [EMetricSpace Œ≤] : EMetricSpace (ProdLp p Œ± Œ≤) :=
  @EMetricSpace.ofT0PseudoEMetricSpace (ProdLp p Œ± Œ≤) _ instT0SpaceProdInstTopologicalSpaceProd

/-- `PseudoMetricSpace` instance on the product of two pseudometric spaces, using the
`L^p` distance, and having as uniformity the product uniformity. -/
instance instPseudoMetricSpace [PseudoMetricSpace Œ±] [PseudoMetricSpace Œ≤] :
    PseudoMetricSpace (ProdLp p Œ± Œ≤) :=
  ((pseudoMetricAux p Œ± Œ≤).replaceUniformity (aux_uniformity_eq p Œ± Œ≤).symm).replaceBornology
    fun s => Filter.ext_iff.1 (aux_cobounded_eq p Œ± Œ≤).symm s·∂ú

/-- `MetricSpace` instance on the product of two metric spaces, using the `L^p` distance,
and having as uniformity the product uniformity. -/
instance instMetricSpace [MetricSpace Œ±] [MetricSpace Œ≤] : MetricSpace (ProdLp p Œ± Œ≤) :=
  MetricSpace.ofT0PseudoMetricSpace _

variable {p Œ± Œ≤}

theorem nndist_eq_sum [PseudoMetricSpace Œ±] [PseudoMetricSpace Œ≤]
    (hp : p ‚â† ‚àû) (x y : ProdLp p Œ± Œ≤) :
    nndist x y = (nndist x.fst y.fst ^ p.toReal + nndist x.snd y.snd ^ p.toReal) ^ (1 / p.toReal) :=
  -- Porting note: was `Subtype.ext`
  NNReal.eq <| by
    push_cast
    exact dist_eq_add (p.toReal_pos_iff_ne_top.mpr hp) _ _

theorem nndist_eq_iSup [PseudoMetricSpace Œ±] [PseudoMetricSpace Œ≤] (x y : ProdLp ‚àû Œ± Œ≤) :
    nndist x y = nndist x.fst y.fst ‚äî nndist x.snd y.snd :=
  -- Porting note: was `Subtype.ext`
  NNReal.eq <| by
    push_cast
    exact dist_eq_sup _ _

variable (p Œ± Œ≤)

theorem lipschitzWith_equiv [PseudoEMetricSpace Œ±] [PseudoEMetricSpace Œ≤] :
    LipschitzWith 1 (ProdLp.equiv p Œ± Œ≤) :=
  lipschitzWith_equiv_aux p Œ± Œ≤

theorem antilipschitzWith_equiv [PseudoEMetricSpace Œ±] [PseudoEMetricSpace Œ≤] :
    AntilipschitzWith ((2 : ‚Ñù‚â•0) ^ (1 / p).toReal) (ProdLp.equiv p Œ± Œ≤) :=
  antilipschitzWith_equiv_aux p Œ± Œ≤

theorem infty_equiv_isometry [PseudoEMetricSpace Œ±] [PseudoEMetricSpace Œ≤] :
    Isometry (ProdLp.equiv ‚àû Œ± Œ≤) :=
  fun x y =>
  le_antisymm (by simpa only [ENNReal.coe_one, one_mul] using lipschitzWith_equiv ‚àû Œ± Œ≤ x y)
    (by
      simpa only [ENNReal.div_top, ENNReal.zero_toReal, NNReal.rpow_zero, ENNReal.coe_one,
        one_mul] using antilipschitzWith_equiv ‚àû Œ± Œ≤ x y)

/-- Seminormed group instance on the product of two normed groups, using the `L^p`
norm. -/
instance instSeminormedAddCommGroup [SeminormedAddCommGroup Œ±] [SeminormedAddCommGroup Œ≤] :
    SeminormedAddCommGroup (ProdLp p Œ± Œ≤) :=
  { Prod.instAddCommGroupSum with
    dist_eq := fun x y => by
      rcases p.dichotomy with (rfl | h)
      ¬∑ simp only [dist_eq_sup, norm_eq_sup, dist_eq_norm]
        rfl
      ¬∑ have : p ‚â† ‚àû := by
          intro hp
          rw [hp, ENNReal.top_toReal] at h
          linarith
        simp only [dist_eq_add (zero_lt_one.trans_le h), norm_eq_add (zero_lt_one.trans_le h),
          dist_eq_norm]
        rfl }

/-- normed group instance on the product of two normed groups, using the `L^p` norm. -/
instance normedAddCommGroup [NormedAddCommGroup Œ±] [NormedAddCommGroup Œ≤] :
    NormedAddCommGroup (ProdLp p Œ± Œ≤) :=
  { ProdLp.instSeminormedAddCommGroup p Œ± Œ≤ with
    eq_of_dist_eq_zero := eq_of_dist_eq_zero }

section norm_of

variable {p Œ± Œ≤}
variable [SeminormedAddCommGroup Œ±] [SeminormedAddCommGroup Œ≤]

theorem nnnorm_eq_add (hp : p ‚â† ‚àû) (f : ProdLp p Œ± Œ≤) :
    ‚Äñf‚Äñ‚Çä = (‚Äñf.fst‚Äñ‚Çä ^ p.toReal + ‚Äñf.snd‚Äñ‚Çä ^ p.toReal) ^ (1 / p.toReal) := by
  ext
  simp [norm_eq_add (p.toReal_pos_iff_ne_top.mpr hp)]

theorem nnnorm_eq_sup (f : ProdLp ‚àû Œ± Œ≤) : ‚Äñf‚Äñ‚Çä = ‚Äñf.fst‚Äñ‚Çä ‚äî  ‚Äñf.snd‚Äñ‚Çä := by
  ext
  norm_cast

theorem norm_eq_of_nat (n : ‚Ñï) (h : p = n) (f : ProdLp p Œ± Œ≤) :
    ‚Äñf‚Äñ = (‚Äñf.fst‚Äñ ^ n + ‚Äñf.snd‚Äñ ^ n) ^ (1 / (n : ‚Ñù)) := by
  have := p.toReal_pos_iff_ne_top.mpr (ne_of_eq_of_ne h <| ENNReal.nat_ne_top n)
  simp only [one_div, h, Real.rpow_nat_cast, ENNReal.toReal_nat, eq_self_iff_true, Finset.sum_congr,
    norm_eq_add this]

theorem norm_eq_of_L2 (x : ProdLp 2 Œ± Œ≤) : ‚Äñx‚Äñ = sqrt (‚Äñx.fst‚Äñ ^ 2 + ‚Äñx.snd‚Äñ ^ 2) := by
  rw [norm_eq_of_nat 2 (by norm_cast) _] -- Porting note: was `convert`
  rw [Real.sqrt_eq_rpow]
  norm_cast

theorem nnnorm_eq_of_L2 (x : ProdLp 2 Œ± Œ≤) : ‚Äñx‚Äñ‚Çä = NNReal.sqrt (‚Äñx.fst‚Äñ‚Çä ^ 2 + ‚Äñx.snd‚Äñ‚Çä ^ 2) :=
  -- Porting note: was `Subtype.ext`
  NNReal.eq <| by
    push_cast
    exact norm_eq_of_L2 x

variable (Œ± Œ≤)

theorem norm_sq_eq_of_L2 (x : ProdLp 2 Œ± Œ≤) : ‚Äñx‚Äñ ^ 2 = ‚Äñx.fst‚Äñ ^ 2 + ‚Äñx.snd‚Äñ ^ 2 := by
  suffices ‚Äñx‚Äñ‚Çä ^ 2 = ‚Äñx.fst‚Äñ‚Çä ^ 2 + ‚Äñx.snd‚Äñ‚Çä ^ 2 by
    simpa only [NNReal.coe_sum] using congr_arg ((‚Üë) : ‚Ñù‚â•0 ‚Üí ‚Ñù) this
  rw [nnnorm_eq_of_L2, NNReal.sq_sqrt]

variable {Œ± Œ≤}

theorem dist_eq_of_L2 (x y : ProdLp 2 Œ± Œ≤) :
    dist x y = (dist x.fst y.fst ^ 2 + dist x.snd y.snd ^ 2).sqrt := by
  simp_rw [dist_eq_norm, norm_eq_of_L2, Pi.sub_apply]
  rfl

theorem nndist_eq_of_L2 (x y : ProdLp 2 Œ± Œ≤) :
    nndist x y = NNReal.sqrt (nndist x.fst y.fst ^ 2 + nndist x.snd y.snd ^ 2) :=
  -- Porting note: was `Subtype.ext`
  NNReal.eq <| by
    push_cast
    exact dist_eq_of_L2 _ _

theorem edist_eq_of_L2 (x y : ProdLp 2 Œ± Œ≤) :
    edist x y = (edist x.fst y.fst ^ 2 + edist x.snd y.snd ^ 2) ^ (1 / 2 : ‚Ñù) := by
  simp [ProdLp.edist_eq_add]

end norm_of

variable [NormedField ùïú] [NormedField ùïú']

section normed_space_inst

variable [SeminormedAddCommGroup Œ±] [NormedSpace ùïú Œ±]
  [SeminormedAddCommGroup Œ≤] [NormedSpace ùïú Œ≤]

-- Porting note: added
instance instModule : Module ùïú (ProdLp p Œ± Œ≤) :=
  { Prod.module with }

/-- The product of two normed spaces is a normed space, with the `L^p` norm. -/
instance instNormedSpace :
    NormedSpace ùïú (ProdLp p Œ± Œ≤) :=
  { instModule p ùïú Œ± Œ≤ with
    norm_smul_le := fun c f => by
      rcases p.dichotomy with (rfl | hp)
      ¬∑ letI : Module ùïú (ProdLp ‚àû Œ± Œ≤) := Prod.module
        suffices ‚Äñc ‚Ä¢ f‚Äñ‚Çä = ‚Äñc‚Äñ‚Çä * ‚Äñf‚Äñ‚Çä by exact_mod_cast NNReal.coe_mono this.le
        simp only [nnnorm_eq_sup, NNReal.mul_sup, ‚Üê nnnorm_smul]
        rfl
      ¬∑ have : p.toReal * (1 / p.toReal) = 1 := mul_div_cancel' 1 (zero_lt_one.trans_le hp).ne'
        have smul_fst : (c ‚Ä¢ f).fst = c ‚Ä¢ f.fst := rfl
        have smul_snd : (c ‚Ä¢ f).snd = c ‚Ä¢ f.snd := rfl
        simp only [norm_eq_add (zero_lt_one.trans_le hp), norm_smul, Real.mul_rpow, norm_nonneg,
          smul_fst, smul_snd]
        rw [‚Üê mul_add, mul_rpow (rpow_nonneg_of_nonneg (norm_nonneg _) _),
          ‚Üê rpow_mul (norm_nonneg _), this, Real.rpow_one]
        positivity }

section towers

variable [NormedSpace ùïú' Œ±] [NormedSpace ùïú' Œ≤]

instance instIsScalarTower [SMul ùïú ùïú'] [IsScalarTower ùïú ùïú' Œ±] [IsScalarTower ùïú ùïú' Œ≤] :
    IsScalarTower ùïú ùïú' (ProdLp p Œ± Œ≤) :=
  Prod.isScalarTower

instance instSMulCommClass [SMulCommClass ùïú ùïú' Œ±] [SMulCommClass ùïú ùïú' Œ≤] :
    SMulCommClass ùïú ùïú' (ProdLp p Œ± Œ≤) :=
  Prod.smulCommClass

end towers

instance instFiniteDimensional [FiniteDimensional ùïú Œ±] [FiniteDimensional ùïú Œ≤] :
    FiniteDimensional ùïú (ProdLp p Œ± Œ≤) :=
  Module.Finite.prod

end normed_space_inst

/- Register simplification lemmas for the applications of `ProdLp` elements, as the usual lemmas
for Prod types will not trigger. -/
variable {ùïú ùïú' p Œ± Œ≤}
variable [SeminormedAddCommGroup Œ±] [NormedSpace ùïú Œ±]
  [SeminormedAddCommGroup Œ≤] [NormedSpace ùïú Œ≤]

section algebra

variable (x y : ProdLp p Œ± Œ≤) (c : ùïú)

@[simp]
theorem zero_fst : (0 : ProdLp p Œ± Œ≤).fst = 0 :=
  rfl

@[simp]
theorem zero_snd : (0 : ProdLp p Œ± Œ≤).snd = 0 :=
  rfl

@[simp]
theorem add_fst : (x + y).fst = x.fst + y.fst :=
  rfl

@[simp]
theorem add_snd : (x + y).snd = x.snd + y.snd :=
  rfl

@[simp]
theorem sub_fst : (x - y).fst = x.fst - y.fst :=
  rfl

@[simp]
theorem sub_snd : (x - y).snd = x.snd - y.snd :=
  rfl

@[simp]
theorem smul_fst : (c ‚Ä¢ x).fst = c ‚Ä¢ x.fst :=
  rfl

@[simp]
theorem smul_snd : (c ‚Ä¢ x).snd = c ‚Ä¢ x.snd :=
  rfl

@[simp]
theorem neg_fst : (-x).fst = -x.fst :=
  rfl

@[simp]
theorem neg_snd : (-x).snd = -x.snd :=
  rfl

end algebra

section Equiv

/-- The canonical map `ProdLp.equiv` between `ProdLp ‚àû Œ≤` and `Œ† i, Œ≤ i` as a linear isometric
equivalence. -/
def equiv‚Çó·µ¢ : ProdLp ‚àû Œ± Œ≤ ‚âÉ‚Çó·µ¢[ùïú] Œ± √ó Œ≤ :=
  { ProdLp.equiv ‚àû Œ± Œ≤ with
    map_add' := fun f g => rfl
    map_smul' := fun c f => rfl
    norm_map' := fun f => by simp }

variable (x y : ProdLp p Œ± Œ≤) (x' y' : Œ± √ó Œ≤) (c : ùïú)

@[simp]
theorem equiv_zero : ProdLp.equiv p Œ± Œ≤ 0 = 0 :=
  rfl

@[simp]
theorem equiv_symm_zero : (ProdLp.equiv p Œ± Œ≤).symm 0 = 0 :=
  rfl

@[simp]
theorem equiv_add : ProdLp.equiv p Œ± Œ≤ (x + y) = ProdLp.equiv p Œ± Œ≤ x + ProdLp.equiv p Œ± Œ≤ y :=
  rfl

@[simp]
theorem equiv_symm_add : (ProdLp.equiv p Œ± Œ≤).symm (x' + y') =
    (ProdLp.equiv p Œ± Œ≤).symm x' + (ProdLp.equiv p Œ± Œ≤).symm y' :=
  rfl

@[simp]
theorem equiv_sub : ProdLp.equiv p Œ± Œ≤ (x - y) = ProdLp.equiv p Œ± Œ≤ x - ProdLp.equiv p Œ± Œ≤ y :=
  rfl

@[simp]
theorem equiv_symm_sub : (ProdLp.equiv p Œ± Œ≤).symm (x' - y') =
    (ProdLp.equiv p Œ± Œ≤).symm x' - (ProdLp.equiv p Œ± Œ≤).symm y' :=
  rfl

@[simp]
theorem equiv_neg : ProdLp.equiv p Œ± Œ≤ (-x) = -ProdLp.equiv p Œ± Œ≤ x :=
  rfl

@[simp]
theorem equiv_symm_neg : (ProdLp.equiv p Œ± Œ≤).symm (-x') = -(ProdLp.equiv p Œ± Œ≤).symm x' :=
  rfl

@[simp]
theorem equiv_smul : ProdLp.equiv p Œ± Œ≤ (c ‚Ä¢ x) = c ‚Ä¢ ProdLp.equiv p Œ± Œ≤ x :=
  rfl

@[simp]
theorem equiv_symm_smul : (ProdLp.equiv p Œ± Œ≤).symm (c ‚Ä¢ x') = c ‚Ä¢ (ProdLp.equiv p Œ± Œ≤).symm x' :=
  rfl

end Equiv

section Single

variable (p Œ± Œ≤)

@[simp]
theorem nnnorm_equiv_symm_fst [hp : Fact (1 ‚â§ p)] (x : Œ±) :
    ‚Äñ(ProdLp.equiv p Œ± Œ≤).symm (x, 0)‚Äñ‚Çä = ‚Äñx‚Äñ‚Çä := by
  induction p using ENNReal.recTopCoe generalizing hp with
  | top =>
    simp [nnnorm_eq_sup]
  | coe p =>
    have hp0 : (p : ‚Ñù) ‚â† 0 := by
      exact_mod_cast (zero_lt_one.trans_le <| Fact.out (p := 1 ‚â§ (p : ‚Ñù‚â•0‚àû))).ne'
    simp [nnnorm_eq_add, NNReal.zero_rpow hp0, ‚Üê NNReal.rpow_mul, mul_inv_cancel hp0]

@[simp]
theorem nnnorm_equiv_symm_snd [hp : Fact (1 ‚â§ p)] (y : Œ≤) :
    ‚Äñ(ProdLp.equiv p Œ± Œ≤).symm (0, y)‚Äñ‚Çä = ‚Äñy‚Äñ‚Çä := by
  induction p using ENNReal.recTopCoe generalizing hp with
  | top =>
    simp [nnnorm_eq_sup]
  | coe p =>
    have hp0 : (p : ‚Ñù) ‚â† 0 := by
      exact_mod_cast (zero_lt_one.trans_le <| Fact.out (p := 1 ‚â§ (p : ‚Ñù‚â•0‚àû))).ne'
    simp [nnnorm_eq_add, NNReal.zero_rpow hp0, ‚Üê NNReal.rpow_mul, mul_inv_cancel hp0]

@[simp]
theorem norm_equiv_symm_fst (x : Œ±) : ‚Äñ(ProdLp.equiv p Œ± Œ≤).symm (x, 0)‚Äñ = ‚Äñx‚Äñ :=
  congr_arg ((‚Üë) : ‚Ñù‚â•0 ‚Üí ‚Ñù) <| nnnorm_equiv_symm_fst p Œ± Œ≤ x

@[simp]
theorem norm_equiv_symm_snd (y : Œ≤) : ‚Äñ(ProdLp.equiv p Œ± Œ≤).symm (0, y)‚Äñ = ‚Äñy‚Äñ :=
  congr_arg ((‚Üë) : ‚Ñù‚â•0 ‚Üí ‚Ñù) <| nnnorm_equiv_symm_snd p Œ± Œ≤ y

@[simp]
theorem nndist_equiv_symm_single_fst (x‚ÇÅ x‚ÇÇ : Œ±) :
    nndist ((ProdLp.equiv p Œ± Œ≤).symm (x‚ÇÅ, 0)) ((ProdLp.equiv p Œ± Œ≤).symm (x‚ÇÇ, 0)) =
      nndist x‚ÇÅ x‚ÇÇ := by
  rw [nndist_eq_nnnorm, nndist_eq_nnnorm, ‚Üê equiv_symm_sub, Prod.mk_sub_mk, sub_zero,
    nnnorm_equiv_symm_fst]

@[simp]
theorem nndist_equiv_symm_single_snd (y‚ÇÅ y‚ÇÇ : Œ≤) :
    nndist ((ProdLp.equiv p Œ± Œ≤).symm (0, y‚ÇÅ)) ((ProdLp.equiv p Œ± Œ≤).symm (0, y‚ÇÇ)) =
      nndist y‚ÇÅ y‚ÇÇ := by
  rw [nndist_eq_nnnorm, nndist_eq_nnnorm, ‚Üê equiv_symm_sub, Prod.mk_sub_mk, sub_zero,
    nnnorm_equiv_symm_snd]

@[simp]
theorem dist_equiv_symm_single_fst (x‚ÇÅ x‚ÇÇ : Œ±) :
    dist ((ProdLp.equiv p Œ± Œ≤).symm (x‚ÇÅ, 0)) ((ProdLp.equiv p Œ± Œ≤).symm (x‚ÇÇ, 0)) =
      dist x‚ÇÅ x‚ÇÇ :=
  congr_arg ((‚Üë) : ‚Ñù‚â•0 ‚Üí ‚Ñù) <| nndist_equiv_symm_single_fst p Œ± Œ≤ x‚ÇÅ x‚ÇÇ

@[simp]
theorem dist_equiv_symm_single_snd (y‚ÇÅ y‚ÇÇ : Œ≤) :
    dist ((ProdLp.equiv p Œ± Œ≤).symm (0, y‚ÇÅ)) ((ProdLp.equiv p Œ± Œ≤).symm (0, y‚ÇÇ)) =
      dist y‚ÇÅ y‚ÇÇ :=
  congr_arg ((‚Üë) : ‚Ñù‚â•0 ‚Üí ‚Ñù) <| nndist_equiv_symm_single_snd p Œ± Œ≤ y‚ÇÅ y‚ÇÇ

@[simp]
theorem edist_equiv_symm_single_fst (x‚ÇÅ x‚ÇÇ : Œ±) :
    edist ((ProdLp.equiv p Œ± Œ≤).symm (x‚ÇÅ, 0)) ((ProdLp.equiv p Œ± Œ≤).symm (x‚ÇÇ, 0)) =
      edist x‚ÇÅ x‚ÇÇ := by
  simp only [edist_nndist, nndist_equiv_symm_single_fst p Œ± Œ≤ x‚ÇÅ x‚ÇÇ]

@[simp]
theorem edist_equiv_symm_single_snd (y‚ÇÅ y‚ÇÇ : Œ≤) :
    edist ((ProdLp.equiv p Œ± Œ≤).symm (0, y‚ÇÅ)) ((ProdLp.equiv p Œ± Œ≤).symm (0, y‚ÇÇ)) =
      edist y‚ÇÅ y‚ÇÇ := by
  simp only [edist_nndist, nndist_equiv_symm_single_snd p Œ± Œ≤ y‚ÇÅ y‚ÇÇ]

end Single

variable (ùïú p Œ± Œ≤)

/-- `ProdLp.equiv` as a linear equivalence. -/
@[simps (config := { fullyApplied := false })]
protected def linearEquiv : ProdLp p Œ± Œ≤ ‚âÉ‚Çó[ùïú] Œ± √ó Œ≤ :=
  { LinearEquiv.refl _ _ with
    toFun := ProdLp.equiv _ _ _
    invFun := (ProdLp.equiv _ _ _).symm }

/-- `ProdLp.equiv` as a continuous linear equivalence. -/
@[simps! (config := { fullyApplied := false }) apply symm_apply]
protected def continuousLinearEquiv : ProdLp p Œ± Œ≤ ‚âÉL[ùïú] Œ± √ó Œ≤ where
  toLinearEquiv := ProdLp.linearEquiv _ _ _ _
  continuous_toFun := continuous_equiv _ _ _
  continuous_invFun := continuous_equiv_symm _ _ _

end ProdLp
